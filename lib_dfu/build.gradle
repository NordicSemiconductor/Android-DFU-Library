apply plugin: 'com.android.library'
apply from: rootProject.file("gradle/git-tag-version.gradle")

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32
        versionCode getVersionCodeFromTags()
        versionName getVersionNameFromTags()
    }

    buildTypes {
        debug {
            minifyEnabled false
            consumerProguardFiles 'dfu-proguard-rules.pro'
            buildConfigField("String", "VERSION_NAME", "\"debug\"")
        }
        release {
            minifyEnabled false
            consumerProguardFiles 'dfu-proguard-rules.pro'
            buildConfigField("String", "VERSION_NAME", "\"${getVersionNameFromTags()}\"")
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.annotation:annotation:1.3.0'

    implementation 'com.google.code.gson:gson:2.8.9'
}

// === Maven Central configuration ===
// The following file exists only when Android DFU Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'dfu'
        POM_NAME = 'DFU Library for Android'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}